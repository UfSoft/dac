<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml"
          xmlns:conversions="org.ufsoft.dac.conversions.*"
          layout="vertical"
          width="100%"
          height="50%"
          title="Conversion Queue"
          creationComplete="creationComplete(event)">

  <mx:Script>
  <![CDATA[

    import net.zengrong.logging.Firebug;

    import mx.core.Application;
    import mx.events.FlexEvent;
    import mx.collections.ArrayCollection;
    import org.ufsoft.dac.events.*;
    import org.ufsoft.dac.conversions.Conversion;

    [Bindable]
    private var conversions:ArrayCollection;

    private function creationComplete(event:FlexEvent):void {
      Application.application.addEventListener(ConnectionEvent.CONNECTED, handleConnectionEvent);
      Application.application.addEventListener(ConnectionEvent.DISCONNECTED, handleConnectionEvent);
      Application.application.addEventListener(ConnectionEvent.CONNECTING, handleConnectionEvent);
      Application.application.addEventListener(ConnectionEvent.RECONNECTING, handleConnectionEvent);

      Application.application.addEventListener(ConversionEvent.NEW, newConversion);
      Application.application.addEventListener(QueueEvent.LIST, handleQueueEvent);
      conversions = new ArrayCollection();
      Firebug.debug("ConvertQueuePanel created", String(conversions));
      Application.application.queryQueueContents();

    }

    private function handleConnectionEvent(event:ConnectionEvent):void {
      if ( event.type == ConnectionEvent.CONNECTED ) {
        // get all conversions
        Application.application.queryQueueContents();
      } else {
        // Lost connection, remove all from queue
        conversions.removeAll();
      }
    }

    private function newConversion(event:ConversionEvent):void {
      Firebug.debug("ConvertQueuePanel newConversion", String(event), String(event.conversion));
      var conversion:Conversion = event.conversion as Conversion;
      Firebug.debug("ConvertQueuePanel newConversion conversion", String(conversion));
      var existingIDs:Array = new Array();
      for ( var i:Number=0; i < conversions.length; i++ ) {
        existingIDs.push(conversions.getItemAt(i).id);
      }
      Firebug.debug("Existing IDS", String(existingIDs), String(conversion.id));
      if ( existingIDs.indexOf(conversion.id) == -1 ) {
        conversions.addItem(conversion);
        Firebug.debug("ConvertQueuePanel newConversion added", String(conversions), String(event.conversion));
      }
    }

    private function handleQueueEvent(event:QueueEvent):void {
      Firebug.debug("Catched QUEUE event", String(event), String(event.conversions));
      conversions = event.conversions;
    }

  ]]>
  </mx:Script>

  <mx:VBox id="conversionsVB" width="100%" height="100%" >
    <mx:Repeater id="conversionsRepeater" dataProvider="{conversions}" recycleChildren="true">
      <conversions:ConversionUI conversion="{conversionsRepeater.currentItem}"/>
    </mx:Repeater>
  </mx:VBox>
</mx:Panel>
